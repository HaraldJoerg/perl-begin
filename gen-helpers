#!/usr/bin/env perl

use strict;
use warnings;
use autodie;

use IO::All qw/ io /;
use HTML::Latemp::GenMakeHelpers ();
use File::Find::Object::Rule     ();

my $DIR = 'lib/make';

sub _exec
{
    my ( $cmd, $err ) = @_;

    if ( system(@$cmd) )
    {
        die $err;
    }
    return;
}

_exec(
    [
        'cookiecutter', '-f', '--no-input',
        'gh:shlomif/cookiecutter--shlomif-latemp-sites',
        'project_slug=.',
    ],
    'cookiecutter failed.'
);

if ( system( "make", "--silent", "-f", "lib/make/build-deps/build-deps.mak" ) )
{
    die "build-deps failed!";
}

if ( -f "rules.mak" )
{
    system( "make", "-s", "src/style.css" );
}
else
{
    system( "compass", "compile" );
}

my $generator = HTML::Latemp::GenMakeHelpers->new(
    'hosts' => [
        {
            'id'         => "common",
            'source_dir' => "common",
            'dest_dir'   => "\$(TARGET)",
        },
        {
            'id'         => "perl_begin",
            'source_dir' => "src",
            'dest_dir'   => "\$(TARGET)",
        },
    ],
    out_dir => $DIR,
);

$generator->process_all();

my $r_fh = io("$DIR/rules.mak");
my $text = $r_fh->slurp();
$text =~
s#^(\$\(PERL_BEGIN_DOCS_DEST\)[^\n]+\n\t)[^\n]+#${1}\$(call PERL_BEGIN_INCLUDE_WML_RENDER)#ms
    or die "Cannot subt";
$text =~
s#^(\$\(PERL_BEGIN_COMMON_DOCS_DEST\)[^\n]+\n\t)[^\n]+#${1}\$(call PERL_BEGIN_COMMON_INCLUDE_WML_RENDER)#ms
    or die "Cannot subt";

my $CP_NEEDLE = 'cp -f $< $@';
$text =~ s#^\t\Q$CP_NEEDLE\E$#\t\$(call COPY)#gms;

$r_fh->print($text);

{
    open my $out_fh, ">", "$DIR/p4n.mak";

    my @targets;
    foreach my $part_idx ( 1 .. 5 )
    {
        # find all the .pm files in @INC
        my @filenames = File::Find::Object::Rule->any(
            ( File::Find::Object::Rule->name(qr/\.svn/)->prune()->discard() ),
            (
                File::Find::Object::Rule->file()->exec(
                    sub {
                        $_[0] ne "index\.html";
                    }
                )
            ),
        )->in("lib/tutorials/perl-for-newbies/lect${part_idx}-all-in-one/");

        foreach my $fn (@filenames)
        {
            my $target = $fn;
            $target =~ s{\Alib/}{};
            $target =~ s{/lect(\d+)-all-in-one/}{/part$1/};
            $target = "\$(PERL_BEGIN_DEST)/$target";

            push @targets, $target;
            print {$out_fh}
                "$target: $fn\n\tmkdir -p `dirname \$\@`\n\tcp -f \$< \$\@\n\n";
        }
    }

    my $makefile_var_name = "DEST_TUTORIALS_PERL_FOR_NEWBIES_TARGETS";
    print {$out_fh} "$makefile_var_name = " . join( " ", @targets ) . "\n\n";
    print {$out_fh} "perl_for_newbies_extra_data: \$($makefile_var_name)\n\n";

    close($out_fh);
}

io->file('Makefile')->print("include $DIR/_Main.mak\n");
